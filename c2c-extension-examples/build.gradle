import org.apache.tools.ant.taskdefs.condition.Os

/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

description = 'Ballerina - Code to Cloud Extension Examples'

configurations {
   bDistributionDir
}

dependencies {
   bDistributionDir project(path: ":c2c-ballerina", configuration: "bDistributionDir")
}

jar {
   enabled = false
}

task buildExamples {
   dependsOn configurations.bDistributionDir
   dependsOn ':c2c-extension-test:prepareDistribution'
   doLast {
      if (!Os.isFamily(Os.FAMILY_WINDOWS)) {
         def distributionLocation = configurations.bDistributionDir.singleFile
         exec {
            workingDir file("${project.projectDir}/examples/c2c-deployment")
            commandLine 'sh', '-c', "$distributionLocation/bin/bal build c2c_deployment.bal"
         }
         exec {
            commandLine 'sh', '-c', "docker rmi c2c_deployment:latest"
         }
         file("${project.projectDir}/examples/c2c-deployment/c2c_deployment.jar").delete()
         file("${project.projectDir}/examples/c2c-deployment/docker").deleteDir()
         file("${project.projectDir}/examples/c2c-deployment/kubernetes").deleteDir()
      }
   }
}

task zipExamples(type: Zip) {
   from 'examples/'
   include '*'
   include '*/*'
   archiveFileName = "c2c-extension-examples-${ballerinaLangVersion}.zip"
   destinationDirectory = file(project.buildDir.absolutePath)
}

publishing {
   publications {
      mavenJava(MavenPublication) {
         artifact source: zipExamples, extension: 'zip'
      }
   }

   repositories {
      maven {
         name = "GitHubPackages"
         url = uri("https://maven.pkg.github.com/ballerina-platform/module-ballerina-c2c")
         credentials {
            username = System.getenv("packageUser")
            password = System.getenv("packagePAT")
         }
      }
   }
}

//zipExamples.dependsOn buildExamples
build.dependsOn zipExamples
