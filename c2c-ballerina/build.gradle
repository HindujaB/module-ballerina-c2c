/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

description = 'Ballerinax - Code2Cloud Extension Annotations'

configurations {
    bDistributionDir
    balTools
    birDir
    kubernetesJar
    extensionPlugin
}

dependencies{
    balTools ("org.ballerinalang:jballerina-tools:${version}"){
        transitive = false
    }
    extensionPlugin project(path: ":c2c-extension", configuration: "shadow")
}

jar {
    enabled = false
}

clean {
    delete "$project.projectDir/output"
    delete "$project.projectDir/target"
    delete "$project.projectDir/Ballerina.lock"
}

def bDistribution = file("$project.buildDir/extracted-distribution/jballerina-tools-${version}")

task unzip {
    doLast {
        configurations.balTools.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File(project.buildDir, "extracted-distribution/")
            }
        }
    }
    outputs.dir bDistribution
}

task unpackStdLibs() {
    doLast {
        configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from project.zipTree(artifact.getFile())
                into new File("${buildDir}/extracted-distribution", artifact.name + "-zip")
            }
        }
    }
}

task copyStdlibs(type: Copy) {
    into bDistribution

    /* Standard Libraries */
    configurations.ballerinaStdLibs.resolvedConfiguration.resolvedArtifacts.each { artifact ->
        def artifactExtractedPath = "${buildDir}/extracted-distribution/" + artifact.name + "-zip"
        into("bir-cache/") {
            from "${artifactExtractedPath}/caches/bir"
        }
        into("bre/lib/") {
            from "${artifactExtractedPath}/libs"
        }
        into("docs/") {
            from "${artifactExtractedPath}/docs"
        }
        // pack to new cache
        into("cache/bir/") {
            from "${artifactExtractedPath}/caches/bir"
        }
        into("cache/balo/") {
            from "${artifactExtractedPath}/caches/balo"
        }
        into("cache/jar/") {
            from "${artifactExtractedPath}/caches/jar"
        }
    }
}

task copyThridPartyDependencies(type: Copy) {
    from configurations.thirdPartyJars
    into "${bDistribution}/bre/lib"
}

def ballerinaToml = file("$project.projectDir/Ballerina.toml")
def semverMatcher = ballerinaToml.getText("UTF-8") =~ /(([0-9]+)\.([0-9]+)\.([0-9]+))/
def tomlSemver = semverMatcher[0][1]

def artifactBirDir = file("$project.projectDir/target/caches/bir_cache/ballerina")
def artifactKubernetesJar = file("$project.projectDir/target/caches/jar_cache/ballerina/c2c/${tomlSemver}/ballerina-c2c-${tomlSemver}.jar")
def artifactParent = file("$project.projectDir/build/target/c2c-ballerina-zip/")
def artifactDocs = file("$project.buildDir/apidocs/docs/")

task ballerinaBuild {
    dependsOn unzip
    inputs.dir file("$project.projectDir/src")
    inputs.file file("$project.projectDir/Ballerina.toml")
    doLast {
        def distributionBinPath = project.projectDir.absolutePath + "/build/extracted-distribution/jballerina-tools-${version}/bin"
        exec {
            workingDir project.projectDir
            environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "$distributionBinPath/ballerina.bat doc -a"
            } else {
                commandLine 'sh', '-c', "$distributionBinPath/ballerina doc -a"
            }
        }
        copy {
            from file("$project.projectDir/target/apidocs/c2c")
            into file("$project.buildDir/apidocs/docs/c2c")
        }
        exec {
            workingDir project.projectDir
            environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "$distributionBinPath/ballerina.bat build -c -a --skip-tests"
            } else {
                commandLine 'sh', '-c', "$distributionBinPath/ballerina build -c -a --skip-tests"
            }
        }
        // Copy the TOML to the same folder as the BIRs
        ["${artifactBirDir}/c2c/${tomlSemver}"].each { birDir ->
            copy {
                from file("$project.projectDir/Ballerina.toml")
                into birDir
            }
        }
        copy {
            from file("$artifactBirDir")
            into file("$artifactParent/caches/bir/ballerina")
        }
        copy {
            from file("$artifactDocs")
            into file("$artifactParent/docs")
        }
        copy {
            from file("$artifactKubernetesJar")
            into file("$artifactParent/libs")
        }
        copy {
            from configurations.extensionPlugin
            into file("${artifactParent}/libs")
        }

    }
    outputs.dir file("$artifactParent")
}

artifacts {
    bDistributionDir file: bDistribution, builtBy: unzip
    birDir file: artifactBirDir, builtBy: ballerinaBuild
    kubernetesJar file: artifactKubernetesJar, builtBy: ballerinaBuild
}

task createArtifactZip(type: Zip) {
    destinationDirectory = file("$buildDir/distributions")
    from ballerinaBuild
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-ballerina-c2c")
            credentials {
                username = System.getenv("packageUser")
                password = System.getenv("packagePAT")
            }
        }
    }
}

unpackStdLibs.dependsOn unzip
copyStdlibs.dependsOn unpackStdLibs
copyThridPartyDependencies.dependsOn copyStdlibs
ballerinaBuild.dependsOn copyThridPartyDependencies
build.dependsOn ballerinaBuild
